---
layout: getting_started
title: 1 Mix and OTP
guide: 1
---

# {{ page.title }}

  <div class="toc"></div>

In this guide, we will learn how to build a complete Elixir application, with its own supervision tree, configuration, tests and more.

The goal of this guide is to build an Elixir application that works as a distributed to-do list. We will partition the to-do list across multiple nodes and also build a simple client which allows us to connect to any of those hosts and send requests such as:

```
CREATE shopping
OK

PUT shopping milk
OK

PUT shopping eggs
OK

LIST shopping
* eggs
* milk
OK

DELETE shopping eggs
OK
```

In order to build our to-do list application, we are going to use three main tools:

* OTP is a set of libraries that ships with Erlang. Erlang developers have been using OTP to build robust, fault-tolerant applications and in this chapter we will explore how many aspects from OTP integrates with Elixir, including supervision trees, event managers and more;

* Mix is a build tool that ships with Elixir that provides tasks for creating, compiling, testing, managing dependencies and much more inside your projects;

* ExUnit is a test-unit based framework that ships with Elixir;

In this chapter, we will create our first project using Mix and explore different features in OTP, Mix and ExUnit as we go.

## 1.1 Our first project 

When you install Elixir, besides getting the `elixir`, `elixirc` and `iex` executables, you also get an executable Elixir script named Mix.

Let's create our first project by invoking `mix new` from the command line, passing the project name as argument, which we are going to call `todo`:

    $ mix new todo

Mix will create a directory named `todo` with few files in it:

    * creating README.md
    * creating .gitignore
    * creating mix.exs
    * creating config
    * creating config/config.exs
    * creating lib
    * creating lib/todo.ex
    * creating test
    * creating test/test_helper.exs
    * creating test/todo_test.exs

Let's take a brief look at those generated files.

> Note: Mix is an Elixir executable. This means that in order to run `mix`, you need to have elixir's executable in your PATH. If not, you can run it by passing the script as argument to elixir:
>
>     $ bin/elixir bin/mix new todo
>
> Note that you can also execute any script in your PATH from Elixir via the -S option:
>
>     $ bin/elixir -S mix new todo
>
> When using -S, elixir finds the script wherever it is in your PATH and executes it.

## 1.2 Project compilation

A file named `mix.exs` was generated inside our new project and it main responsibility is to configure our project. Let's take a look at it (comments removed):

```elixir
defmodule Todo.Mixfile do
  use Mix.Project

  def project do
    [app: :todo,
     version: "0.0.1",
     deps: deps]
  end

  def application do
    [applications: []]
  end

  defp deps do
    []
  end
end
```

Our `mix.exs` defines two public functions: `project`, which returns project configuration like the project name and version, and `application`, which is used to generate an Erlang application that is managed by the Erlang Runtime.

There is also a private function named `deps`, that is invoked from the `project` function, that defines our application dependencies. There is no requiremet to define `deps` as a separate function, although it helps with keeping the project configuration tidy.

Mix also generated a file at `lib/todo.ex` with a simple module definition:

```elixir
defmodule Todo do
end
```

This structure is enough to compile our project:

    $ mix compile

Will generate:

    Compiled lib/todo.ex
    Generated todo.app

Notice the `lib/todo.ex` file was compiled but, not only that, a `todo.app` file was generated too. This `.app` file is generated with the information from the `application/0` function in the `mix.exs` file and we will explore it with details in future chapters.

Once the project is compiled, you can start an iex session inside the project by running:

    $ iex -S mix

## 1.3 Running tests

Mix also generated the appropriate structure for running our project tests. Mix projects usually follow the convention of having a `_test.exs` file in the `test` directory for each file in the `lib` directory. For this reason, we can already find a `test/todo_test.exs` file, which doesn't do much at this point:

```elixir
defmodule TodoTest do
  use ExUnit.Case

  test "the truth" do
    assert true
  end
end
```

It is important to note a couple things:

1) Notice the test file is an Elixir script file (`.exs`). This is convenient because we don't need to compile test files before running them;

2) We define a test module named `TodoTest`, using [`ExUnit.Case`](/docs/stable/ex_unit/ExUnit.Case.html) to inject default behavior and define a simple test using the `test/2` macro;

Mix also genererated a file named `test/test_helper.exs` which is responsible for setting up the test framework:

```elixir
ExUnit.start
```

This file will be automatically required by Mix every time before we run our tests. We can run tests with `mix test`:

    Compiled lib/todo.ex
    Generated todo.app
    .

    Finished in 0.04 seconds (0.04s on load, 0.00s on tests)
    1 tests, 0 failures

    Randomized with seed 540224

Notice that by running `mix test`, Mix has compiled the source files and generated the application file once again. This happens because Mix supports multiple environments, which we will explore in the next section.

Furthermore, you can see that ExUnit prints a dot for each successful test and automatically randomize tests too. Let's make the test fail on purpose and see what happens.

Change the assertion in `test/todo_test.exs` to the following:

```elixir
assert 1 + 1 == 3
```

Now run `mix test` again (notice this time there was no compilation):

    1) test the truth (TodoTest)
       test/todo_test.exs:4
       Assertion with == failed
       code: 1 + 1 == 3
       lhs:  2
       rhs:  3
       stacktrace:
         test/todo_test.exs:5

    Finished in 0.05 seconds (0.05s on load, 0.00s on tests)
    1 tests, 1 failures

For each failure, ExUnit prints a detailed report, containing the test name, the test case, the code that failed and the values for the left-hand side (lhs) and right-hand side (rhs) of the `==` operator.

In the second line of the failure, right below the test name, there is also the location the test was defined (while the stacktrace relates to the location of the failure itself). If you copy the test location in the second line, containing the file and line, and paste it in-front of `mix test`, Mix will load and run just that particular test:

    $ mix test test/todo_test.exs:4

This shortcut will prove to be extremely useful as we build our project, allowing us to quickly iterate by running just a specific set of tests.

## 1.4 Environments

Mix has the concept of environments that allows a developer to customize compilation and other options based on an external setting. By default, Mix understands three environments:

* `dev` - the one in which mix tasks are run by default;
* `test` - used by `mix test`;
* `prod` - the environment in which dependencies are loaded and compiled;

By default, these environments behave the same and all configuration we have seen so far will affect all three environments. Customization per environment can be done by accessing `Mix.env`:

```elixir
def project do
  [deps_path: deps_path(Mix.env)]
end

defp deps_path(:prod), do: "prod_deps"
defp deps_path(_), do: "deps"
```

Mix will default to the `dev` environment, except the `test` task that will default to the `test` environment. The environment can be changed via the `MIX_ENV` environment variable:

    $ MIX_ENV=prod mix compile

In the next chapters, we will learn more about building OTP applications with Mix and how to create your own tasks.

## 1.5 Exploring

There is much more to Mix but we will explore it as we build our project. Keep in mind though that, at any moment, you can invoke `mix help` which will list all available tasks:

    $ mix help

You can get further information about a particular by invoking `mix help TASK`.

Let's start to write some code.
